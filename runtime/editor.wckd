import draw
import input
import color

import editor_code
import editor_gfx

function debug_print(str)
    asm "
        ld.arg 0
        conv.str
        syscall.byname \"debug_print\"
        pop
    "
end

enum Modes
    Code, Gfx, Map, Sfx, Music
end

var mode = Modes.Code

class BarButton
    var caption = ""
    var x, y, w, h
    var bgUp, bgDown

    function new(x, y, w, h, caption = "", bgUp = color.Names.Gray, bgDown = color.Names.DarkGray)
        this.caption = caption
        this.x = x ; this.y = y ; this.w = w ; this.h = h
        this.bgUp = bgUp ; this.bgDown = bgDown
    end

    function draw(down = false)
        if down then
            draw.fillRect(this.x + 1, this.y + 1, this.w - 2, this.h - 2, this.bgDown) # fill
            draw.fillRect(this.x + 1, this.y, this.w - 2, 1, color.Names.DarkGray) # top
            draw.fillRect(this.x, this.y + 1, 1, this.h - 2, color.Names.DarkGray) # left
            draw.fillRect(this.x + this.w - 1, this.y + 1, 1, this.h - 2, color.Names.LightGray) # right
            draw.fillRect(this.x + 1, this.y + this.h - 1, this.w - 2, 1, color.Names.LightGray) # bottom
            draw.setFont("7px")
            draw.text(this.x + 3, this.y + 3, this.caption, color.Names.White)
        else
            draw.fillRect(this.x + 1, this.y + 1, this.w - 2, this.h - 2, this.bgUp) # fill
            draw.fillRect(this.x + 1, this.y, this.w - 2, 1, color.Names.LightGray) # top
            draw.fillRect(this.x, this.y + 1, 1, this.h - 2, color.Names.LightGray) # left
            draw.fillRect(this.x + this.w - 1, this.y + 1, 1, this.h - 2, color.Names.DarkGray) # right
            draw.fillRect(this.x + 1, this.y + this.h - 1, this.w - 2, 1, color.Names.DarkGray) # bottom
            draw.setFont("7px")
            draw.text(this.x + 3, this.y + 3, this.caption, color.Names.White)
        end
    end

    function hitTest(down, x, y)
        if x >= this.x && x <= this.x + this.w then
            if y >= this.y && y <= this.y + this.h then
                if down then
                    this.draw(true)
                else
                    debug_print("click " + this.caption + "\n")
                    this.draw(true)
                end
                return true
            end
        end
        return false
    end
end

var codeButton = new BarButton(2, 2, 20, 12, "P", color.Names.Gray, color.Names.DarkGray)
var gfxButton = new BarButton(24, 2, 20, 12, "G", color.Names.ForestGreen, color.Names.DarkGreen)
var mapButton = new BarButton(46, 2, 20, 12, "M", color.Names.DarkRed, color.Names.Bordeaux)
var sfxButton = new BarButton(68, 2, 20, 12, "S", color.Names.LightBrown, color.Names.Brown)
var musicButton = new BarButton(90, 2, 20, 12, "M", color.Names.SteelBlue, color.Names.DarkBlue)

var leftButtonDown = false
var projectName = "(unnamed)"

export function update()
    draw.cls(color.Names.Black)

    # editor BG
    draw.fillRect(0, 16, 256, 180, color.Names.DarkGray)

#    draw.text(100, 90, "mouse_x: " + input.mouseX())
#    draw.text(100, 100, "mouse_y: " + input.mouseY())

    var mouse_x = input.mouseX(), mouse_y = input.mouseY()
    if input.leftButtonPressed() then
        leftButtonDown = true
        codeButton.hitTest(true, mouse_x, mouse_y)
        gfxButton.hitTest(true, mouse_x, mouse_y)
        mapButton.hitTest(true, mouse_x, mouse_y)
        sfxButton.hitTest(true, mouse_x, mouse_y)
        musicButton.hitTest(true, mouse_x, mouse_y)
    else if leftButtonDown then
        leftButtonDown = false
        if codeButton.hitTest(false, mouse_x, mouse_y) then mode = Modes.Code ;
        if gfxButton.hitTest(false, mouse_x, mouse_y) then mode = Modes.Gfx ;
        if mapButton.hitTest(false, mouse_x, mouse_y) then mode = Modes.Map ;
        if sfxButton.hitTest(false, mouse_x, mouse_y) then mode = Modes.Sfx ;
        if musicButton.hitTest(false, mouse_x, mouse_y) then mode = Modes.Music ;
    end

    if mode == Modes.Code then editor_code.update() ;
    if mode == Modes.Gfx then editor_gfx.update() ;

    # tabbar
    var barColor
    if mode == Modes.Code then barColor = color.Names.BlueGray
    if mode == Modes.Gfx then barColor = color.Names.ForestGreen
    if mode == Modes.Map then barColor = color.Names.DarkRed
    if mode == Modes.Sfx then barColor = color.Names.LightBrown
    if mode == Modes.Music then barColor = color.Names.Blue

    draw.fillRect(0, 0, 256, 16, barColor) # background
    codeButton.draw(mode == Modes.Code)
    gfxButton.draw(mode == Modes.Gfx)
    mapButton.draw(mode == Modes.Map)
    sfxButton.draw(mode == Modes.Sfx)
    musicButton.draw(mode == Modes.Music)

    draw.setFont("9px")
    draw.text(256 - 6 - draw.textWidth(projectName), 4, projectName, color.Names.LightGray)

    if input.keyPress(input.Keys.Escape) then
        return true
    end
end
