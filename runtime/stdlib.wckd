(prototypeof array).length = arrayLength
function arrayLength()
    var length = 0
    asm "
        ld.local 0
        ld.mapitem \"value\"
        arr.len
        st.local 1
    "
    return length
end

(prototypeof string).length = stringLength
function stringLength()
    var length = 0
    asm "
        ld.local 0
        ld.mapitem \"value\"
        str.len
        st.local 1
    "
    return length
end

(prototypeof array).deleteAt = arrayDeleteAt
function arrayDeleteAt(index)
    asm "
        ld.local 0
        ld.arg 0
        del.arrelem
    "
end

(prototypeof array).insertAt = arrayInsertAt
function arrayInsertAt(index, value)
    asm "
        ld.arg 1
        ld.local 0
        ld.arg 0
        arr.insert
    "
end

(prototypeof string).toLowerCase = stringToLowerCase
function stringToLowerCase()
    var copy = 0
    asm "
        ld.local 0
        ld.mapitem \"value\"
        ld.str \"\"
        str.concat
        st.local 1
    "
    for i in 0..copy.length() - 1 do
        if copy[i] >= 65 && copy[i] <= 90 then
            copy[i] += 32;
        end
    end
    return copy
end

(prototypeof array).contains = arrayContains
function arrayContains(val)
    for e in this.value do
        if e == val then return true;
    end
    return false
end

(prototypeof string).startsWith = stringStartsWith
function stringStartsWith(str)
    return this.value[0:str.length() - 1] == str
end

(prototypeof string).indexOf = stringIndexOf
function stringIndexOf(str)
    var len = str.length()
    for i in 0..this.length() do
        if this.value[i:i + len] == str then
           return i
        end
    end
    return empty
end

(prototypeof string).split = stringSplit
function stringSplit(pat)
    var str = this.value
    var parts = []
    var i
    while (i = str.indexOf(pat)) != empty do
        parts[] = str[0:i]
        str = str[i + pat.length():-1]
    end
    parts[] = str
    return parts
end